/*
 *  air flights management platform running example expressed in the GDM language
 */
 /***********************/
/* Entities definition */
/***********************/

entity Aircraft {
  id registrationNumber unique      /*code of an aircraft*/
  modelNumber text                  /*its model number*/
  manufacturer text                 /*name of its manufacturer*/
  capacity int                      /*its number of seats*/
  ref Flight[*] supportedFlights
}

entity Flight {
  id code unique                    /*flight number*/
  departureTime time                /*the time in format hh:mm at which a flight is scheduled to depart from a given airport of origin*/
  departureDate date                /*its scheduled date*/
  arrivalTime time                  /*the time in hh:mm format at which a flight must land at a given destination airport*/
  arrivalDate date                  /*its scheduled date*/
  level int                         /*Flight level. It is an aircraft's altitude at standard air pressure, expressed in hundreds of feet*/
  ref Localization[*] registeredPositions
  ref WebSiteAccess[*] sitesAccessed
  ref Passenger[*] registeredPersons
  ref Aircraft[1] usedAircraft
  ref Airport[1] origin
  ref Airport[1] destination
}


entity Passenger {
  id idPassport unique      /*Passport number of a passenger*/
  firstname text            /*its first name*/
  lastname text             /*its last name*/
  birthdate date            /*its birthdate in format mm-jj-aaaa*/
  sex text                  /*its sex: "M" for male, "F" for Female*/
  nationality text          /*its nationality*/
  ref Flight[*] flights
  ref WebSiteAccess[*] accessedSites
}


entity Airport {
  id codeICAO unique        /*code created by the International Civil Aviation Organization to identify the airports in the world*/
  nameAirport text          /*name of the airport*/
  city text                 /*city where the airport is located*/
  country text              /*country where the airport is located*/
  ref Flight[*] landedflights
  ref Flight[*] departedflights
}

entity Localization {
  timeLocaliz time          /*time of calculation of the position of the aircraft*/
  dateLocaliz date          /*its date of calculation*/
  latitude text             /*latitude of the aircraft at a given time*/
  longitude text            /*its longitude*/
  ref Flight[1] inFlight
}

entity WebSiteAccess {
  timeAccess datetime           /*time of a passenger's access to a web site within a flight*/
  url : string                  /*url of the web site visited*/
  ref Flight[1] relatedFlight
  ref Passenger[1] requester
}

/**********************/
/* Queries definition */
/**********************/

Query Q1_aircraftsCapacityWithin:
SELECT registrationNumber,capacity
FROM Aircraft
WHERE capacity>="?" and capacity<="?"

Query Q2_airportsGivenCountrySortedByCities:
SELECT nameAirport,CodeICAO,city
FROM Airport
WHERE country="?"
ORDER BY city ASC

Query Q3_passengersOfGivenFlight:
SELECT firstName,lastName,idPassport
FROM Passenger
INCLUDE Passenger.flights as FL
WHERE FL.code="?"

query Q4_passengersDepartingGivenCountry:
SELECT Origin.city,Destination.city,FL.departureTime,idPassport,firstName,lastName,birthdate,sex,nationality
FROM Passenger
INCLUDE Passenger.flights as FL,Passenger.flights.Origin as Origin,Passenger.flights.Destination as Destination
WHERE FL.departureDate="?" and Origin.country="?"
ORDER BY Origin.city ASC,FL.departureTime ASC

Query Q5_passengersDepartingGivenPeriod:
SELECT Origin.country,Origin.city,FL.departureTime,FL.code,idPassport,firstName,lastName,birthdate,sex,nationality
FROM Passenger
INCLUDE Passenger.flights as FL,Passenger.flights.Origin as Origin
WHERE FL.departureDate="?"
ORDER BY Origin.country ASC,Origin.city ASC

Query Q6_webAccessesOfGivenFlight:
SELECT P.idPassport,P.firstName,P.lastName,access.timeAccess
FROM Flight
INCLUDE Flight.registeredPersons as P,Flight.registeredPersons.accessedSites as access
WHERE code="?"
ORDER BY P.idPassport ASC,access.timeAccess DESC

Query Q7_aircraftsDepartedGivenAirport:
SELECT registrationNumber,modelNumber,manufacturer,capacity,FL.departureDate,FL.departureTime
FROM Aircraft
INCLUDE Aircraft.supportedFlights as FL,Aircraft.supportedFlights.Origin as A
WHERE FL.departureTime>="?" and FL.departureDate>="?" and FL.arrivalTime<="?" and FL.arrivalDate<="?" and A.codeICAO="?"

Query Q8_localizationAircraft:
SELECT AC.registrationNumber,AC.modelNumber,POS.latitude,POS.longitude,POS.dateLocaliz,POS.timeLocaliz
FROM Flight
INCLUDE Flight.registeredPositions as POS,Flight.usedAirCraft as AC
WHERE Flight.code="?"
ORDER BY POS.dateLocaliz DESC,POS.timeLocaliz DESC 

Query Q9_monitoringAircraftsFlightsLevels:
SELECT code,level,ORIGIN.city,DESTINATION.city
FROM Flight
INCLUDE Flight.Origin as ORIGIN,Flight.Destination as DESTINATION
WHERE Flight.departureDate="?" or Flight.arrivalDate="?"
ORDER BY Flight.level ASC
